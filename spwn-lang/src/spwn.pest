
COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!"\n" ~ ANY)* ~ "\n")}
WHITESPACE = _{ " " | "\t"}


stmt  = _{
    def | add_obj | call | retrn | implement | if_stmt | expr
}

def = {(symbol | "*") ~ "=" ~ (expr) ~ native_prop*}
call = {variable ~ "!"}
retrn = {"return" ~ expr?}
add_obj = {"add" ~ expr}
implement = {"impl" ~ variable ~ dictionary}

if_stmt = {"if" ~ expr ~ ss* ~ cmp_stmt ~ ss* ~ ("else" ~ ss* ~ (cmp_stmt | if_else))?}
if_else = {if_stmt}

native_prop = {"#" ~ (private /*| id_define*/) ~ arguments}
    private =   {"private"}
    //id_define = "id_define"


cmp_stmt = {
    "{" ~ ss* ~ (stmt ~ ss+)* ~ stmt? ~ "}"
}

dictionary = {
    "{" ~ ss* ~ ((def) ~ ss+)* ~ (def)? ~ "}"
}

array = {
    "[" ~ ss* ~ (expr ~ "," ~ ss*)* ~ expr? ~ ss* ~ "]"
}

obj = {
    "obj" ~ "{" ~ ss* ~ (obj_prop ~ "," ~ ss*)* ~ obj_prop? ~ ss* ~ "}"
}

macro_def = {arguments_def ~ cmp_stmt}

obj_prop = {expr ~ ":" ~ expr}

import = {"import" ~ variable}


value_literal = _{
    import | id | number | obj | macro_def | dictionary | 
    cmp_stmt | bool | symbol | string | 
    array | "(" ~ expr ~")"
}

variable = {value_literal ~ (("." ~ symbol) | index | arguments)*}

arg = {symbol ~ "=" ~ expr | expr}
arguments = { "(" ~ (arg ~ ",")* ~ arg? ~ ")" }
index = {"[" ~ expr ~ "]"}

arg_def = {symbol ~ ":" ~ expr | symbol}
arguments_def = {
    "(" ~ (arg_def ~ ",")* ~ arg_def? ~ ")"
}

number = @{"-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? }

expr = {(variable ~ operator)* ~ variable}

operator = {("||" | "&&" | "==" | "!=") | 
			(">" | "<" | ">=" | "<=") | 
            ("/" | "*") | 
            ("+" | "-")} 

bool = {"true" | "false"}

id = {
    (number | "?") ~ className
}

className = {"g" | "b" | "c" | "i"}

symbol = ${ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT | "_")*}

string = {("\"" ~ (!"\"" ~ ANY)* ~ "\"") | ("'" ~ (!"'" ~ ANY)* ~ "'")}

ss = _{(("\r" | "\n") | ";") ~ ("\r" | "\n")*}

spwn = { SOI ~ ss* ~ (stmt ~ ss+)* ~ stmt? ~ EOI }

